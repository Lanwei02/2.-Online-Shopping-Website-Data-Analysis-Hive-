--Create tables in Hive
CREATE TABLE users (swid STRING, birth_dt STRING, gender_cd CHAR(1))
  ROW FORMAT DELIMITED
  FIELDS TERMINATED BY '\t'
  STORED AS TEXTFILE
  TBLPROPERTIES ("skip.header.line.count"="1");
 
  
CREATE TABLE products (url STRING, category STRING)
ROW FORMAT DELIMITED
FIELDS TERMINATED BY '\t'
STORED AS TEXTFILE
TBLPROPERTIES ("skip.header.line.count"="1");


CREATE TABLE omniturelogs (col_1 STRING,col_2 STRING,col_3 STRING,col_4 STRING,col_5 STRING,col_6 STRING,col_7 STRING,col_8 STRING,col_9 STRING,col_10 STRING,col_11 STRING,col_12 STRING,col_13 STRING,col_14 STRING,col_15 STRING,col_16 STRING,col_17 STRING,col_18 STRING,col_19 STRING,col_20 STRING,col_21 STRING,col_22 STRING,col_23 STRING,col_24 STRING,col_25 STRING,col_26 STRING,col_27 STRING,col_28 STRING,col_29 STRING,col_30 STRING,col_31 STRING,col_32 STRING,col_33 STRING,col_34 STRING,col_35 STRING,col_36 STRING,col_37 STRING,col_38 STRING,col_39 STRING,col_40 STRING,col_41 STRING,col_42 STRING,col_43 STRING,col_44 STRING,col_45 STRING,col_46 STRING,col_47 STRING,col_48 STRING,col_49 STRING,col_50 STRING,col_51 STRING,col_52 STRING,col_53 STRING)
ROW FORMAT DELIMITED
FIELDS TERMINATED by '\t'
STORED AS TEXTFILE
TBLPROPERTIES ("skip.header.line.count"="1");



-- load data into the tables
LOAD DATA INPATH '/hive_exercise/users' OVERWRITE INTO TABLE users;
LOAD DATA INPATH '/hive_exercise/products' OVERWRITE INTO TABLE products;
LOAD DATA INPATH '/hive_exercise/omniture-logs' OVERWRITE INTO TABLE omniturelogs;

select * from users LIMIT 10;
-- Outcome:
-- 0001BDD9-EABF-4D0D-81BD-D9EABFCD0D7D 8-Apr-84 F
-- 00071AA7-86D2-4EB9-871A-A786D27EB9BA 7-Feb-88 F
-- 00071B7D-31AF-4D85-871B-7D31AFFD852E 22-Oct-64 F
-- 0007967E-F188-4598-9C7C-E64390482CFB 1-Jun-66 M
-- 000B90B2-92DC-4A7A-8B90-B292DC9A7A71 13-Jun-84 M
-- 000C1856-994E-476B-8C18-56994E676B29 29-Dec-80 U
-- 000F36E5-9891-4098-9B69-CEE78483B653 24-Mar-85 F
-- 00102F3F-061C-4212-9F91-1254F9D6E39F 1-Nov-91 F
-- 0010C6F2-8C04-450E-90C6-F28C04B50E97 20-Jun-02 U
-- 0011C945-28C4-4D6F-B1E6-6CA7EFC14548 13-Nov-87 F


select * from products LIMIT 10;
-- Outcome:
-- http://www.acme.com/ books
-- http://www.acme.com/SH55126545/VD55149415 movies
-- http://www.acme.com/SH55126545/VD55163347 games
-- http://www.acme.com/SH55126545/VD55165149 electronics
-- http://www.acme.com/SH55126545/VD55166807 computers
-- http://www.acme.com/SH55126545/VD55170364 home&garden
-- http://www.acme.com/SH55126545/VD55173061 handbags
-- http://www.acme.com/SH55126545/VD55177927 clothing
-- http://www.acme.com/SH55126545/VD55179433 shoes
-- http://www.acme.com/SH55126554/VD55147564 outdoors


CREATE VIEW omniture AS
SELECT col_2 ts, col_8 ip, col_13 url, col_14 swid, col_50 city, col_51 country, col_53 state
FROM omniturelogs;

select * from omniture LIMIT 10;
-- Outcome:
-- 2012-03-15 01:34:46 69.76.12.213 http://www.acme.com/SH55126545/VD55177927 {8D0E437E-9249-4DDA-BC4F-C1E5409E3A3B} coeur d alene usa id
-- 2012-03-15 17:23:53 67.240.15.94 http://www.acme.com/SH55126545/VD55166807 {E3FEBA62-CABA-11D4-820E-00A0C9E58E2D} queensbury usa ny
-- 2012-03-15 17:05:00 67.240.15.94 http://www.acme.com/SH55126545/VD55149415 {E3FEBA62-CABA-11D4-820E-00A0C9E58E2D} queensbury usa ny
-- 2012-03-15 01:27:53 98.234.107.75 http://www.acme.com/SH55126545/VD55179433 {49E0D2EE-1D57-48C5-A27D-7660C78CB55C} sunnyvale usa ca
-- 2012-03-15 02:09:38 75.85.165.38 http://www.acme.com/SH55126545/VD55179433 {F6F8B460-4204-4C26-A32C-B93826EDCB99} san diego usa ca
-- 2012-03-15 10:18:02 71.53.206.175 http://www.acme.com/SH55126545/VD55166807 {AC4D6E52-2F0B-48E2-9CA0-770B68222711} charlottesville usa va
-- 2012-03-15 11:38:42 97.96.62.161 http://www.acme.com/SH55126545/VD55179433 {56C1008E-6095-48E1-A58B-9523459F295C} parrish usa fl
-- 2012-03-15 12:42:59 129.119.158.240 http://www.acme.com/SH55126545/VD55170364 {CC5F240A-9B9E-4288-B7AF-91521D6F7A1C} dallas usa tx
-- 2012-03-15 11:02:49 96.241.99.50 http://www.acme.com/SH55126545/VD55179433 {303AADFF-4CB8-480C-A757-213BEEB051BC} capitol heights usa md
-- 2012-03-15 11:14:20 96.241.99.50 http://www.acme.com/SH55126545/VD55179433 {303AADFF-4CB8-480C-A757-213BEEB051BC} capitol heights usa md



-- Simple Join:

SELECT o.swid, o.city, o.country, o.state, p.category
FROM omniture AS o, products AS p 
WHERE o.url = p.url
LIMIT 20;


-- Left outer join:
SELECT users.gender_cd, users.birth_dt
FROM omniture JOIN products ON (omniture.url = products.url) LEFT OUTER JOIN users ON (omniture.swid = concat('{', users.swid, '}'))
LIMIT 10;

-- Step 15:
CREATE TABLE WEBLOGANALYTICS AS
SELECT o.ts Logdate, o.url URL, o.ip IP, o.city CITY, UPPER(o.state) STATE, o.country COUNTRY, p.category CATEGORY, FLOOR(datediff(from_unixtime(unix_timestamp(o.ts, 'yyyy-MM-dd HH:mm:ss'),'yyyy-MM-dd'), from_unixtime(unix_timestamp(u.birth_dt,'dd-MMM-yy'),'yyyy-MM-dd'))/365.25) CURRENT_AGE, u.gender_cd GENDER_CODE
FROM omniture o JOIN products p ON (o.url = p.url) JOIN users u ON (o.swid = concat('{', u.swid, '}'));


-- Step 16:
SELECT GENDER_CODE GENDER_CD, CITY, CATEGORY, COUNT(*) AS AMOUNT
FROM WEBLOGANALYTICS
WHERE STATE == 'TX' AND CURRENT_AGE > 19 AND CURRENT_AGE < 45
GROUP BY GENDER_CODE, CITY, CATEGORY;
